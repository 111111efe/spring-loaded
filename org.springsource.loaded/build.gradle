group = "org.springsource.loaded"
version = '1.1.4-dev'
jar.baseName = 'springloaded'

compileJava {
	options.debug = true
}

configurations {
	testCompileOnly
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.8'
}

dependencies {
	tools 'com.googlecode.jarjar:jarjar:1.3'

	compile 'asm:asm:3.2'
	compile 'asm:asm-tree:3.2'
	
	testCompile 'junit:junit:4.11'
	
	testCompileOnly files("../org.springsource.loaded.testdata.groovy/groovy-1.8.2.jar")
	testCompileOnly project(':org.springsource.loaded.testdata')
	testCompileOnly project(':org.springsource.loaded.testdata.groovy')
	testCompileOnly project(':org.springsource.loaded.testdata.plugin')
	testCompileOnly project(':org.springsource.loaded.testdata.subloader')
	testCompileOnly project(':org.springsource.loaded.testdata.superloader')
}

sourceSets {  
    test {  
        compileClasspath += configurations.testCompileOnly  
    }  
}  

test {
	jvmArgs "-noverify"
	ignoreFailures true
	systemProperties["springloaded.tests.useGradleBuildDir"] = true
	systemProperties["springloaded.tests.generatedTests"] = System.getProperty("generatedTests", "false")
}

task jarAll(type: Jar, dependsOn: jar) {
	manifest {
		attributes(
				"Specification-Vendor": "SpringSource",
				"Specification-Title": "SpringLoaded Agent",
				"Specification-Version": version,
				"Implementation-Vendor": "SpringSource",
				"Implementation-Title": "org.springsource.loaded",
				"Implementation-Version": version,
				"Premain-Class": "org.springsource.loaded.agent.SpringLoadedAgent",
				"Can-Redefine-Classes": "true"
				)
	}

    // What is the syntax here to include the license in the jar? this doesn't work
    // from '.' include 'LICENSES/LICENSE'

    inputs.files jar.archivePath
    classifier = 'agent'
	
    doLast {
        project.ant {
            taskdef name: "jarjar", classname: "com.tonicsystems.jarjar.JarJarTask", classpath: configurations.tools.asPath
            jarjar(jarfile: archivePath, manifest: "$temporaryDir/MANIFEST.MF") {
                zipfileset(src: jar.archivePath)
                configurations.compile.files.each { jarjarFile ->
                    zipfileset(src: jarjarFile)
                }
                rule pattern: "org.objectweb.asm.**", result: "sl.org.objectweb.asm.@1"
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {     
	classifier = 'sources'     
	from sourceSets.main.allSource 
}

// add the jars as artifacts
artifacts {      
	archives sourcesJar      
	archives jarAll
}
